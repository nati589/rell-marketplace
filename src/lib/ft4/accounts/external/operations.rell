
function delete_auth_descriptor_message(gtv) {
    val params = struct<delete_auth_descriptor>.from_gtv(gtv);
    return "Please sign the message\nto delete auth descriptor:\n%s\n\nfrom account\n{account_id}".format(params.auth_descriptor_id);
}

function delete_auth_descriptor_resolver(args: gtv, account_id: byte_array, auth_descriptor_ids: list<byte_array>) {
    val params = struct<delete_auth_descriptor>.from_gtv(args);
    if (params.auth_descriptor_id in auth_descriptor_ids) return params.auth_descriptor_id;

    for (ad in accounts.account_auth_descriptor @* { .account.id == account_id, .id in auth_descriptor_ids }) {
        if (accounts.has_flags(ad, ["A"])) return ad.id;
    }

    return null;
}

@extend(auth.auth_handler)
function () = auth.add_overridable_auth_handler(
    scope = rell.meta(delete_auth_descriptor).mount_name,
    flags = [],
    message = delete_auth_descriptor_message(*),
    resolver = delete_auth_descriptor_resolver(*)
);

operation delete_auth_descriptor(auth_descriptor_id: byte_array) {
    val account = auth.authenticate();
    accounts._delete_auth_descriptor(
        accounts.auth_descriptor_by_id(account, auth_descriptor_id)
    );
}

function delete_auth_descriptors_for_signer_message(gtv) {
    val params = struct<delete_auth_descriptors_for_signer>.from_gtv(gtv);
    return "Please sign the message\nto delete all auth descriptor for signer:\n%s\n\nfrom account\n{account_id}".format(params.signer);
}

function delete_auth_descriptors_for_signer_resolver(args: gtv, account_id: byte_array, auth_descriptor_ids: list<byte_array>) {
    val params = struct<delete_auth_descriptors_for_signer>.from_gtv(args);

    val ads = (a_ad: accounts.account_auth_descriptor, ad_s: accounts.auth_descriptor_signer) @* {
        a_ad.account.id == account_id,
        ad_s.account_auth_descriptor == a_ad,
        ad_s.id == params.signer,
        a_ad.auth_type == accounts.auth_type.S,
        a_ad.id in auth_descriptor_ids
    } (a_ad.id);
    if (not ads.empty()) {
        return ads[0];
    }

    for (ad in accounts.account_auth_descriptor @* { .account.id == account_id, .id in auth_descriptor_ids }) {
        if (accounts.has_flags(ad, ["A"])) return ad.id;
    }

    return null;
}

@extend(auth.auth_handler)
function () = auth.add_overridable_auth_handler(
    scope = rell.meta(delete_auth_descriptors_for_signer).mount_name,
    flags = [],
    message = delete_auth_descriptors_for_signer_message(*),
    resolver = delete_auth_descriptors_for_signer_resolver(*)
);

operation delete_auth_descriptors_for_signer(signer: byte_array) {
    val account = auth.authenticate();

    val ads = (a_ad: accounts.account_auth_descriptor, ad_s: accounts.auth_descriptor_signer) @* {
        a_ad.account == account,
        ad_s.account_auth_descriptor == a_ad,
        ad_s.id == signer,
        a_ad.auth_type == accounts.auth_type.S
    } (a_ad);
    for (ad in ads) {
        accounts._delete_auth_descriptor(ad);
    }
}

function delete_all_auth_descriptors_exclude_message(gtv) {
    val params = struct<delete_all_auth_descriptors_exclude>.from_gtv(gtv);
    return "Please sign the message\nto delete all auth descriptors excluding:\n%s\n\n from account:\n{account_id}".format(params.auth_descriptor_id);
}

function delete_all_auth_descriptors_exclude_resolver(args: gtv, account_id: byte_array, auth_descriptor_ids: list<byte_array>) {
    val params = struct<delete_all_auth_descriptors_exclude>.from_gtv(args);
    if (not (params.auth_descriptor_id in auth_descriptor_ids)) return null;
    val auth_descriptor = accounts.account_auth_descriptor @? {
        .account.id == account_id,
        .id == params.auth_descriptor_id
    };

    if (empty(auth_descriptor)) return null;
    return if (accounts.has_flags(auth_descriptor, ["A"])) auth_descriptor.id else null;
}

@extend(auth.auth_handler)
function () = auth.add_overridable_auth_handler(
    scope = rell.meta(delete_all_auth_descriptors_exclude).mount_name,
    flags = ["A"],
    message = delete_all_auth_descriptors_exclude_message(*),
    resolver = delete_all_auth_descriptors_exclude_resolver(*)
);

operation delete_all_auth_descriptors_exclude(account_id: byte_array, auth_descriptor_id: byte_array) {
    val account = accounts.account_by_id(account_id);
    val auth_descriptor = accounts.auth_descriptor_by_id(account, auth_descriptor_id);
    auth._ft_auth(["A"], account, auth_descriptor);

    accounts._delete_all_auth_descriptors_exclude(auth_descriptor);
}

function add_auth_descriptor_message(gtv) {
    val params = struct<add_auth_descriptor>.from_gtv(gtv);
    val flags = params.new_desc.args[0];

    return "Please sign the message\nto add auth descriptor\nwith flags:\n%s\n\nto account:\n{account_id}".format(flags);
}

@extend(auth.auth_handler)
function () = auth.add_overridable_auth_handler(
    scope = rell.meta(add_auth_descriptor).mount_name,
    flags = ["A"],
    message = add_auth_descriptor_message(*)
);

operation add_auth_descriptor(new_desc: accounts.auth_descriptor) {
    val account = auth.authenticate();
    accounts.add_auth_descriptor_to_account(account, new_desc);
}
