import ^.assets;
import ^.accounts;

entity asset_origin {
   key assets.asset;
   origin_blockchain_rid: byte_array;
}

entity blockchain_account {
    key accounts.account;
}

function is_blockchain(account_id: byte_array): boolean {
   return blockchain_account @? { .account.id == account_id }??;
}

function ensure_blockchain_account(blockchain_rid: byte_array): accounts.account {
   val acc = accounts.account @? { .id == blockchain_rid };
   if (acc??) {
      require(
         exists(blockchain_account @? { acc }),
         "The account already exists, but it's not a blockchain account"
      );
      return acc;
   } else {
      val bc_acc = create accounts.account (id = blockchain_rid);
      create blockchain_account ( bc_acc );
      return bc_acc;
   }
}

function get_blockchain_account(blockchain_rid: byte_array): accounts.account {
   return require(
      accounts.account @? { blockchain_rid },
      "Account not found"
   );
}

namespace Unsafe {
    function register_crosschain_asset(
        name,
        symbol: text,
        decimals: integer,
        issuing_blockchain_rid: byte_array,
        icon_url: text,
        origin_blockchain_rid: byte_array
    ): asset_origin {
        require(origin_blockchain_rid.size() > 0, "origin_blockchain_rid cannot be empty");
        val asset = assets.Unsafe.register_asset(
                name,
                symbol,
                decimals,
                issuing_blockchain_rid,
                icon_url
            );
        return create asset_origin(asset, origin_blockchain_rid);
    }
}
